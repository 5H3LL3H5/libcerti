## $Id: configure.in,v 3.10 2005/04/30 13:58:42 breholee Exp $ 
## ---------------------------------------------------------------------------

## Process this file with autoconf to produce `configure'

## Init
AC_INIT([RTIG/RTIG.cc])
AC_PREREQ(2.53)
AC_REVISION($Revision: 3.10 $)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE([certi], `cat $srcdir/VERSION`)

## Programs and system
AC_PROG_YACC
AM_PROG_LEX
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_CANONICAL_HOST
case x$host_os in
     xsolaris*) AC_DISABLE_SHARED ;;
     *-*-cygwin*) WITH_CYGWIN="-DWITH_CYGWIN" ;;
esac
AC_SUBST(WITH_CYGWIN)
AC_PROG_LIBTOOL
YACC="$YACC -d "

## Autoheader
AH_TEMPLATE([socklen_t], [Define the type for socklen_t])

## Unix / C++
ETR_SOCKET_NSL
TYPE_SOCKLEN_T
AC_FUNC_SELECT_ARGTYPES
AC_CXX_HAVE_NUMERIC_LIMITS
CXXFLAGS="$CXXFLAGS -DRTI_USES_STD_FSTREAM "

# X11
AC_PATH_XTRA
if test $no_x; then
    	AC_MSG_WARN([X11 files not found]);
else
	X_LIBRARY_PATH=$x_libraries
	XCFLAGS="$X_CFLAGS"	
	XLFLAGS="$X_LIBS"
	XLIBS="-lX11 -L$X_LIBRARY_PATH $X_EXTRA_LIBS"
	WITH_X11="-DWITH_X11"
	lib_search_path="$lib_search_path $XLFLAGS"
	inc_search_path="$inc_search_path $XCFLAGS"
	AC_SUBST(X_LIBRARY_PATH)
	AC_SUBST(XLIBS)
	AC_SUBST(XCFLAGS)
	AC_SUBST(WITH_X11)
fi

# XML
AC_CHECK_PROG(XML2_CONFIG, xml2-config, xml2-config)
if test "x$XML2_CONFIG" != x ; then
    XML_LIBS="`$XML2_CONFIG --libs`"
    XML_CFLAGS="`$XML2_CONFIG --cflags`"
    HAVE_XML="-DHAVE_XML"
    AC_SUBST(XML_LIBS)
    AC_SUBST(XML_CFLAGS)
    AC_SUBST(HAVE_XML)
fi

# Output

AC_OUTPUT([Makefile include/Makefile libCERTI/Makefile RTIG/Makefile
RTIA/Makefile libRTI/Makefile test/Makefile test/libgraphc/Makefile
test/Billard/Makefile doc/Makefile])

## $Id: configure.in,v 3.10 2005/04/30 13:58:42 breholee Exp $
